apply plugin: 'maven'
apply plugin: 'signing'

version = "0.3"

def isReleaseBuild() {
  !version.contains("SNAPSHOT")
}

def sonatypeRepositoryUrl

if (isReleaseBuild()) {
  println "PROCESSOR RELEASE BUILD $version"
  sonatypeRepositoryUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
} else {
  println "PROCESSOR DEBUG BUILD $version"
  sonatypeRepositoryUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
}

def username = project.getProperty("sonatype.username") ?: ""
def password = project.getProperty("sonatype.password") ?: ""

afterEvaluate { project ->
  uploadArchives {
    repositories {
      mavenDeployer {
        beforeDeployment { deployment -> signing.signPom(deployment) }

        repository(url: sonatypeRepositoryUrl) {
          authentication(userName: username, password: password)
        }

        pom.project {
          name 'Android AutoParcel Processor'
          groupId 'me.tatarka'
          description 'Port of a port of Google AutoValue for the Android platform to work seamlessly with gson'
          url 'https://github.com/evant/auto-parcel-gson'
          inceptionYear '2015'

          scm {
            url 'https://github.com/evant/auto-parcel-gson'
            connection 'scm:https://evant@github.com/evant/auto-parcel-gson.git'
            developerConnection 'scm:git@github.com:evant/auto-parcel-gson.git'
          }

          licenses {
            license {
              name 'The Apache Software License, Version 2.0'
              url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
              distribution 'repo'
            }
          }

          developers {
            developer {
              id 'evant'
              name 'Evan Tatarka'
            }
          }
        }
      }
    }
  }

  signing {
    required { isReleaseBuild() && gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
  }

  task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
  }

  task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
  }

  artifacts {
    archives sourcesJar
    archives javadocJar
  }
}
